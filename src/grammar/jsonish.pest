jsonish = { SOI ~ value ~ EOI }

object = {
  "{" ~ "}" |
  "{" ~ pair ~ ("," ~ pair)* ~ ","? ~ "}"
}

pair = {
  string ~ ":" ~ value |
  identifier_name ~ ":" ~ value
}

array = {
  "[" ~ "]" |
  "[" ~ value ~ ("," ~ value)* ~ ","? ~ "]"
}

value = _{ object | array | string | number | boolean | null }


string = ${ double_string | single_string }

double_string = { "\"" ~ inner_double_string ~ "\"" }
inner_double_string = @{ double_char* }

single_string = { "'" ~ inner_single_string ~ "'" }
inner_single_string = @{ single_char* }

boolean = { "true" | "false" }
null = { "null" }

double_char = {
  !("\"" | "\\") ~ ANY
  | escape_char
  | unicode_escape_sequence
}

single_char = {
  !("'" | "\\") ~ ANY
  | escape_char
  | unicode_escape_sequence
}

escape_char = { "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t") }

identifier_name = @{ identifier_start ~ identifier_part*  }
identifier_start = { unicode_letter | "$" | "_" | unicode_escape_sequence }
identifier_part = { identifier_start | ASCII_DIGIT | CONNECTOR_PUNCTUATION }

unicode_escape_sequence = { "\\u" ~ ASCII_HEX_DIGIT{4} }
unicode_letter = { LETTER | NUMBER }

number = @{
  "-"?
  ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
  ~ ("." ~ ASCII_DIGIT*)?
  ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
