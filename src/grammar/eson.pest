json = { SOI ~ (object | array) ~ EOI }

object = {
  "{" ~ "}" |
  "{" ~ pair ~ ("," ~ pair)* ~ ","? ~ "}"
}

pair = {
  string ~ ":" ~ value |
  identifier_name ~ ":" ~ value
}

array = {
  "[" ~ "]" |
  "[" ~ value ~ ("," ~ value)* ~ ","? ~ "]"
}

value = _{ object | array | string | number | boolean | null }


string = { "\"" ~ inner_string ~ "\"" }
inner_string = @{ char* }
boolean = { "true" | "false" }
null = { "null" }

char = {
  !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | unicode_escape_sequence
}

identifier_name = @{ identifier_start ~ identifier_part*  }
identifier_start = _{ unicode_letter | "$" | "_" | unicode_escape_sequence }
identifier_part = _{ identifier_start | ASCII_DIGIT | CONNECTOR_PUNCTUATION }

unicode_escape_sequence = @{ "\\u" ~ ASCII_HEX_DIGIT{4} }
unicode_letter = _{ LETTER | NUMBER }

number = @{
  "-"?
  ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
  ~ ("." ~ ASCII_DIGIT*)?
  ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
